; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "TrafficMonitor"
!define PRODUCT_VERSION "1.0"
!define PRODUCT_PUBLISHER "TrafficMonitor"
!define PRODUCT_WEB_SITE "https://github.com/zhongyang219/TrafficMonitor"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\TrafficMonitor.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "..\TrafficMonitor\res\TrafficMonitor.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Language Selection Dialog Settings
!define MUI_LANGDLL_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_LANGDLL_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "NSIS:Language"

; Welcome page
!insertmacro MUI_PAGE_WELCOME

!define MUI_PAGE_CUSTOMFUNCTION_SHOW SetDirectory
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!define MUI_PAGE_CUSTOMFUNCTION_SHOW KillMainProcess
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\TrafficMonitor.exe"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

LangString UNINSTALL_CONFIRM 1033 "Do you really want to remove $(^Name) completely, and all of its components?"
LangString UNINSTALL_CONFIRM 2052 "你确实要完全移除 $(^Name) ，其及所有的组件？"
LangString UNINSTALL_CONFIRM 1028 "你_要完全移除 $(^Name) ，其及所有的M件？"

LangString UNINSTALL_SUCCESS 1033 "$(^Name) was successfully removed from your computer."
LangString UNINSTALL_SUCCESS 2052 "$(^Name) 已成功地从你的计算机移除。"
LangString UNINSTALL_SUCCESS 1028 "$(^Name) 已成功地哪愕挠算C移除。"

;如果想修改资源文件的目录，修改此处
!define FileResourceDir "..\Bin\Release"
!define MAIN_EXE_NAME "TrafficMonitor.exe"
; Language files
!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "SimpChinese"
!insertmacro MUI_LANGUAGE "TradChinese"
; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "TrafficMonitorSetup.exe"
InstallDir "$PROGRAMFILES\TrafficMonitor"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Section "MainSection" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite try
  File "${FileResourceDir}\LibreHardwareMonitorLib.dll"
  File "${FileResourceDir}\LibreHardwareMonitorLib.xml"
  File "${FileResourceDir}\OpenHardwareMonitorApi.dll"
  SetOutPath "$INSTDIR\plugins"
  File "${FileResourceDir}\plugins\PluginDemo.dll"
  SetOutPath "$INSTDIR"
  File "${FileResourceDir}\TrafficMonitor.exe"
  CreateDirectory "$SMPROGRAMS\TrafficMonitor"
  CreateShortCut "$SMPROGRAMS\TrafficMonitor\TrafficMonitor.lnk" "$INSTDIR\TrafficMonitor.exe"
  CreateShortCut "$DESKTOP\TrafficMonitor.lnk" "$INSTDIR\TrafficMonitor.exe"
SectionEnd

Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\TrafficMonitor\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\TrafficMonitor\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\TrafficMonitor.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\TrafficMonitor.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
 WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "InstallLocation" "$INSTDIR"
SectionEnd

Function SelectLanguage
  ;根据windowsapi返回值选择语言，不支持语言默认显示英文
  System::Call 'Kernel32::GetUserDefaultUILanguage() i.r0'
  ${If} $0 == '1033'
  ${OrIf} $0 == '1028'
  ${OrIf} $0 == '2052'
      StrCpy $LANGUAGE $0
  ${Else}
      StrCpy $LANGUAGE 1033
  ${EndIf}
FunctionEnd

!AddPluginDir "./plugins"

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK  "$(UNINSTALL_SUCCESS)"
FunctionEnd

Function .onInit
  Call SelectLanguage
FunctionEnd

Function un.onInit
      System::Call 'Kernel32::GetUserDefaultUILanguage() i.r0'
  ${If} $0 == '1033'
  ${OrIf} $0 == '1028'
  ${OrIf} $0 == '2052'
      StrCpy $LANGUAGE $0
  ${Else}
      StrCpy $LANGUAGE 1033
  ${EndIf}
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "$(UNINSTALL_CONFIRM)" IDYES +2
  Abort
  
  nsProcessW::_FindProcess "${MAIN_EXE_NAME}"
  Pop $R0
  IntCmp $R0 0 running no_running no_running
  running:
  nsProcessW::_KillProcess "${MAIN_EXE_NAME}"
  no_running:
FunctionEnd

;主要处理覆盖安装场景
Function SetDirectory
  ReadRegStr $0 ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "InstallLocation"

  ${If} $0 != ""
    Strcpy $INSTDIR $0
    ;禁用浏览按钮
    FindWindow $0 "#32770" "" $HWNDPARENT
    GetDlgItem $0 $0 1001
    EnableWindow $0 0
    ;禁止编辑目录
    FindWindow $0 "#32770" "" $HWNDPARENT
    GetDlgItem $0 $0 1019
    EnableWindow $0 0

    SendMessage $0 ${WM_SETTEXT} 0 "STR:$INSTDIR"
  ${endif}
FunctionEnd

Function KillMainProcess
  nsProcessW::_FindProcess "${MAIN_EXE_NAME}"
  Pop $R0
  IntCmp $R0 0 running no_running no_running
  running:
  nsProcessW::_KillProcess "${MAIN_EXE_NAME}"
  no_running:
  GoTo endding
  endding:
FunctionEnd

Section Uninstall
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\TrafficMonitor.exe"
  Delete "$INSTDIR\plugins\PluginDemo.dll.ini"
  Delete "$INSTDIR\plugins\PluginDemo.dll"
  Delete "$INSTDIR\OpenHardwareMonitorApi.dll"
  Delete "$INSTDIR\LibreHardwareMonitorLib.xml"
  Delete "$INSTDIR\LibreHardwareMonitorLib.dll"
  Delete "$INSTDIR\global_cfg.ini"
  Delete "$INSTDIR\config.ini"

  Delete "$SMPROGRAMS\TrafficMonitor\Uninstall.lnk"
  Delete "$SMPROGRAMS\TrafficMonitor\Website.lnk"
  Delete "$DESKTOP\TrafficMonitor.lnk"
  Delete "$SMPROGRAMS\TrafficMonitor\TrafficMonitor.lnk"

  RMDir "$SMPROGRAMS\TrafficMonitor"
  RMDir "$INSTDIR\plugins"
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
SectionEnd